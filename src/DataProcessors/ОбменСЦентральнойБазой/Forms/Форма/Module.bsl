#Область ОписаниеПеременных
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Перем ЗащищенноеСоединение; //Вид соединения. ЗащищенноеСоединениеOpenSSL или Неопределено
	Перем ПараметрыПодключения; //Параметры подключения. Структура
	Перем Соединение; //HTTP-Соединение дляя подключения к центральной базе. HTTPСоединение
	Перем НачалоАдресаСервера; //Часть строки адреса центральной базы начиная после "://"
	Перем АдресБезПротокола; // Часть строки адреса центральной базы без указания протокола соединения
	Перем НачалоРазделителя; // Часть строки центральной базы без указания протокола соединения до "/"
	Перем АдресСервера; //Адрес Сервера центральной базы
	Перем ИмяБазыНаСервере; //Имя центральной базы на сервере
	Перем ЗапросHTTP; //HTTP-запрос для подключения
	Перем Заголовки; //Заголовки HTTP-запроса. Соответствие
	Перем Ping; //HTTP-Сервис Д_ОбменСМобильнымКлиентом Шаблон URL Ping Метод GET
	Перем ОтветСервера; //Ответ сервера со сведениями о статусе подключения
	Перем ОтветСервераСтрока; //Ответ сервера со сведениями о статусе подключения. Строка
// Конец Володин А.А. 23 октября 2024 г.
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АдресЦентральнойБазы.Значение КАК АдресЦентральнойБазы,
		|	Логин.Значение КАК Логин,
		|	Пароль.Значение КАК Пароль
		|ИЗ
		|	Константа.АдресЦентральнойБазы КАК АдресЦентральнойБазы,
		|	Константа.Логин КАК Логин,
		|	Константа.Пароль КАК Пароль";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		АдресЦентральнойБазы = ВыборкаДетальныеЗаписи.АдресЦентральнойБазы;
		//Логин = ВыборкаДетальныеЗаписи.Логин; //Заполнение логина реализовано по Текущему пользователю
		Пароль = ВыборкаДетальныеЗаписи.Пароль;
	КонецЦикла;
// Конец Володин А.А. 23 октября 2024 г. 
//Начало Володин А.А. 28 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	Логин = ПользователиИнформационнойБазы.ТекущийПользователь(); //Включить использование после настройки Роли Курьер
//Конец Володин А.А. 28 октября 2024 г.
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресЦентральнойБазыПриИзменении(Элемент)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ЗаписатьАдресЦентральнойБазыВКонстанту(АдресЦентральнойБазы);
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаКлиенте
Процедура ЛогинПриИзменении(Элемент)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	//ЗаписатьЛогинВКонстанту(Логин); //Отключить после настройки роли Курьер
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ЗаписатьПарольВКонстанту(Пароль);
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьСоединение(Команда)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ПроверитьСоединениеНаСервере();
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЧислоЗаказов(Команда)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ПолучитьЧислоЗаказовНаСервере();
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокДоставок(Команда)
//Начало Володин А.А. 28 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ПолучитьСписокДоставокНаСервере();
//Конец Володин А.А. 28 октября 2024 г.
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьФотоДоставок(Команда)
//Начало Володин А.А. 08 ноября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ПередатьФотоДоставокНаСервере();
//Конец Володин А.А. 08 ноября 2024 г.
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьАдресЦентральнойБазыВКонстанту(АдресБазы)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Константы.АдресЦентральнойБазы.Установить(АдресБазы);
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЛогинВКонстанту(Логин)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	//Константы.Логин.Установить(Логин); //Отключить после настройки роли Курьер
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПарольВКонстанту(Пароль)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Константы.Пароль.Установить(Пароль);	
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаСервере
Функция ЗапросНаСервер(АдресБазы, АдресСервиса)
//Начало Володин А.А. 23 октября 2024 г.
//Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ЗапросHTTP = ФормированиеЗапроса(АдресБазы, АдресСервиса);
	
	ОтветСервера = Соединение.Получить(ЗапросHTTP.ЗапросHTTP);
	
	Возврат ОтветСервера;
//Конец Володин А.А. 23 октября 2024 г.
КонецФункции

&НаСервере
Функция ФормированиеЗапроса(АдресБазы, АдресСервиса)
//Начало Володин А.А. 08 ноября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1
	//Определим параметры подключения:
	ПараметрыПодключения = ПолучитьПараметрыПодключения(АдресБазы);
	
	//Создадим соединение:
	Соединение = УстановитьСоединение(ПараметрыПодключения);
	
	//Создадим и отправим HTTP-Запрос:
	Заголовки = Новый Соответствие();
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИДКлиента = СистемнаяИнформация.ИдентификаторКлиента;
	
	//Найдем или создадим Централиный узел. Выборка будет формироваться для этого узла:
	Узел = ПланыОбмена.ОбменСМобильнымУстройством.НайтиПоКоду("main");
	
	Если Не ЗначениеЗаполнено(Узел) Тогда
		
		УзелОбъект = ПланыОбмена.ОбменСМобильнымУстройством.СоздатьУзел();
		УзелОбъект.Код = "main";
		УзелОбъект.Наименование = "Центральный узел";
		УзелОбъект.Записать();
		
		Узел = УзелОбъект.Ссылка;
		
	КонецЕсли;
	
	//Проверим заполненность данных текущего узла:
	ТекущийУзел = ПланыОбмена.ОбменСМобильнымУстройством.ЭтотУзел();
	
	Если Не ЗначениеЗаполнено(ТекущийУзел.Код) Тогда
		
		УзелОбъект = ТекущийУзел.ПолучитьОбъект();
		УзелОбъект.Код = ИДКлиента;
		УзелОбъект.Наименование = ИДКлиента;
		УзелОбъект.Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
		УзелОбъект.Записать();
		
	КонецЕсли;
	
	Заголовки.Вставить("x_MobileID", Строка(ИДКлиента));
	
	ЗапросHTTP = Новый HTTPЗапрос(ПараметрыПодключения.ИмяБазыНаСервере + АдресСервиса, Заголовки);
	
	Запрос = Новый Структура;
	Запрос.Вставить("ЗапросHTTP", ЗапросHTTP);
	Запрос.Вставить("Узел", Узел);
	
	Возврат Запрос;
//Конец Володин А.А. 08 ноября 2024 г.
КонецФункции

&НаСервере
Функция ПолучитьПараметрыПодключения(АдресБазы)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Результат = Новый Структура;
	
	Если Лев(АдресБазы, 5) = "https" Тогда
		
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
		
	Иначе
		
		ЗащищенноеСоединение = Неопределено;
		
	КонецЕсли;
	
	НачалоАдресаСервера = СтрНайти(АдресБазы, "://") + 3;
	АдресБезПротокола = Сред(АдресБазы, НачалоАдресаСервера);
	НачалоРазделителя = СтрНайти(АдресБезПротокола, "/");
	
	АдресСервера = Лев(АдресБезПротокола, НачалоРазделителя - 1);
	ИмяБазыНаСервере = Сред(АдресБезПротокола, НачалоРазделителя);
	
	Результат.Вставить("АдресСервера", АдресСервера);
	Результат.Вставить("ИмяБазыНаСервере", ИмяБазыНаСервере);
	Результат.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	
	Возврат Результат;
// Конец Володин А.А. 23 октября 2024 г.
КонецФункции

&НаСервере
Функция УстановитьСоединение(ПараметрыПодключения)
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	Соединение = Новый HTTPСоединение(ПараметрыПодключения.АдресСервера, , ПараметрыПодключения.Логин, ПараметрыПодключения.Пароль, , ПараметрыПодключения.ЗащищенноеСоединение);
	
	Возврат Соединение;
// Конец Володин А.А. 23 октября 2024 г.
КонецФункции

&НаСервере
Процедура ПолучитьЧислоЗаказовНаСервере()
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройства 
	ОтветСервера = ЗапросНаСервер(АдресЦентральнойБазы, "/hs/mobile/delivery");
	
	ОтветСервераСтрока = ОтветСервера.ПолучитьТелоКакСтроку();
	
	Если ОтветСервера.КодСостояния <> 200 Тогда
		
		Сообщить(СтрШаблон("Error: %1", ОтветСервераСтрока));
		
		Возврат;
		
	Иначе
		
		//МассивДоставок = ПрочитатьЗначениеJSON(ОтветСервераСтрока); //Метод доступен начиная с версии 8.3.23
		//ЧтениеJSON = Новый ЧтениеJSON;
		//ЧтениеJSON.УстановитьСтроку(ОтветСервераСтрока);
		//МассивДоставок = ПрочитатьJSON(ЧтениеJSON);
		//ЧтениеJSON.Закрыть();
		//
		//КоличествоНовыхЗаказов = 0;
		//
		//Для Каждого Доставка Из МассивДоставок Цикл
		//
		//Сообщить(СтрШаблон("Новый заказ от %1 № %2.
		//						|Заказчик: %3.
		//						|Комментарий: %4",
		//						Доставка.Дата, Доставка.Номер, Доставка.Контрагент, Доставка.Комментарий));
		//	
		//	КоличествоНовыхЗаказов = КоличествоНовыхЗаказов + 1;
		//
		//КонецЦикла;
		
		Сообщить(СтрШаблон("У вас заказов: %1", ОтветСервераСтрока));
		
	КонецЕсли
// Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоединениеНаСервере(); 
// Начало Володин А.А. 23 октября 2024 г.
// Задача 1 ДЗ Обмен с центральной базой и использование возможностей мобильного устройств
	ОтветСервера = ЗапросНаСервер(АдресЦентральнойБазы, "/hs/mobile/ping");
	
	ОтветСервераСтрока = ОтветСервера.ПолучитьТелоКакСтроку(); //При обмене с внешними сервисами необходимо указывать кодировку и раскодировать
	
	Если ОтветСервера.КодСостояния <> 200 Тогда
		
		Сообщить(СтрШаблон("Error: %1", ОтветСервераСтрока));
		
		Возврат;
		
	Иначе
		
		Сообщить(СтрШаблон("Ok: %1", ОтветСервераСтрока));
		
	КонецЕсли
//Конец Володин А.А. 23 октября 2024 г.
КонецПроцедуры

&НаСервере
Процедура ПолучитьСписокДоставокНаСервере();
//Начало Володин А.А. 28 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ОтветСервера = ЗапросНаСервер(АдресЦентральнойБазы, "/hs/mobile/exchangedelivery");
	
	ОтветСервераСтрока = ОтветСервера.ПолучитьТелоКакСтроку();
	
	Если ОтветСервера.КодСостояния <> 200 Тогда
		
		ВызватьИсключение СтрШаблон("Ошибка: %1", ОтветСервераСтрока);
		
		Возврат;
		
	Иначе
		
		//МассивДоставок = ПрочитатьЗначениеJSON(ОтветСервераСтрока); //Метод доступен начиная с версии 8.3.23
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ОтветСервераСтрока);
		МассивДоставок = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		КоличествоСозданных = 0;
		КоличествоОбновленных = 0;
		
		Для Каждого Доставка Из МассивДоставок Цикл
		
		//Получим ссылки на документ Доставка и Контрагента:
		ИДДоставки = Новый УникальныйИдентификатор(Доставка.Ссылка);
		ИДКонтрагента = Новый УникальныйИдентификатор(Доставка.КонтрагентСсылка);
		
		СсылкаДоставка = Документы.Доставка.ПолучитьСсылку(ИДДоставки);
		СсылкаКонтрагент = Справочники.Контрагенты.ПолучитьСсылку(ИДКонтрагента);
		
		//Получим объекты документа доставка по ссылке:
		ОбъектДоставка = СсылкаДоставка.ПолучитьОбъект();
		
			//Проверим есть ли такой документ в нашей базе:
			Если ОбъектДоставка = Неопределено Тогда
			
				//Создадим новый документ:
				НоваяДоставка = Документы.Доставка.СоздатьДокумент();
				НоваяДоставка.УстановитьСсылкуНового(СсылкаДоставка);
				НоваяДоставка.Дата = Дата(Доставка.Дата);
				НоваяДоставка.Номер = Доставка.Номер;
				НоваяДоставка.АдресДоставки = Доставка.АдресДоставки;
				НоваяДоставка.Комментарий = Доставка.Комментарий;
				НоваяДоставка.Статус = Перечисления.СтатусыДоставок[СтрЗаменить(Доставка.Статус, " ", "")];
				НоваяДоставка.Контрагент = ПолучитьКонтрагента(СсылкаКонтрагент, Доставка.КонтрагентНаименование);
				
				//Запишем новые строки:
				ЗаполнитьТабличнуюЧасть(Доставка.Товары, НоваяДоставка);
				
				НоваяДоставка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				
				КоличествоСозданных = КоличествоСозданных + 1;
				
			Иначе
				
				ОбъектДоставка = СсылкаДоставка.ПолучитьОбъект();
				//Внесем изменения в существующий:
				ОбъектДоставка.Дата = Дата(Доставка.Дата);
				ОбъектДоставка.Номер = Доставка.Номер;
				ОбъектДоставка.АдресДоставки = Доставка.АдресДоставки;
				ОбъектДоставка.Комментарий = Доставка.Комментарий;
				ОбъектДоставка.Статус = Перечисления.СтатусыДоставок[СтрЗаменить(Доставка.Статус, " ", "")];
				ОбъектДоставка.Контрагент = ПолучитьКонтрагента(СсылкаКонтрагент, Доставка.КонтрагентНаименование);
				
				//Очистим существующие строки:
				ОбъектДоставка.Товары.Очистить();
				
				//Запишем новые строки:
				ЗаполнитьТабличнуюЧасть(Доставка.Товары, ОбъектДоставка);
				
				ОбъектДоставка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				
				КоличествоОбновленных = КоличествоОбновленных + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Сообщить(СтрШаблон("Обмен документами выполнен. Создано %1, Обновлено %2", КоличествоСозданных, КоличествоОбновленных));
		
	КонецЕсли
//Конец Володин А.А. 28 октября 2024 г.
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьТабличнуюЧасть(Структура, Документ)
//Начало Володин А.А. 29 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	//Заполним табличную часть:
	Для Каждого Строка Из Структура Цикл
		
		//Получим ссылку на Номенклатуру:
		ИДНоменклатуры = Новый УникальныйИдентификатор(Строка.НоменклатураСсылка);
		СсылкаНоменклатура = Справочники.Номенклатура.ПолучитьСсылку(ИДНоменклатуры);
		
		НоваяСтрокаТЧ = Документ.Товары.Добавить();
		НоваяСтрокаТЧ.Номенклатура = ПолучитьНоменклатуру(СсылкаНоменклатура, Строка.НоменклатураНаименование, Строка.ВидНоменклатуры);
		НоваяСтрокаТЧ.Количество = Строка.Количество;
		
	КонецЦикла;
//Конец Володин А.А. 29 октября 2024 г.
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуру(Ссылка, Наименование, Вид)
//Начало Володин А.А. 29 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ОбъектНоменклатура = Ссылка.ПолучитьОбъект();
	
	//Проверим есть ли в базе такая номенклатура:
	Если ОбъектНоменклатура = Неопределено Тогда
		
		//Создадим новую Номенклатуру:
		НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
		НоваяНоменклатура.УстановитьСсылкуНового(Ссылка);
		НоваяНоменклатура.Наименование = Наименование;
		НоваяНоменклатура.ВидНоменклатуры = Перечисления.ВидНоменклатуры[Вид];
		НоваяНоменклатура.Записать();
		
		//Вернем ссылку на новую номенклатуру:
		Возврат НоваяНоменклатура.Ссылка;
		
	Иначе
		
		//Вернем ссылку на существующую номенклатуру:
		Возврат Ссылка;
		
	КонецЕсли;
//Конец Володин А.А. 29 октября 2024 г.
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтрагента(Ссылка, Наименование)
//Начало Володин А.А. 29 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ОбъектКонтрагент = Ссылка.ПолучитьОбъект();
	
	//Проверим есть ли контрагент в нашей базе:
	Если ОбъектКонтрагент = Неопределено Тогда
		
		//Создадим нового контрагента:
		НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		НовыйКонтрагент.УстановитьСсылкуНового(Ссылка);
		НовыйКонтрагент.Наименование = Наименование;
		НовыйКонтрагент.Записать();
		
		//Вернем созданного контрагента:
		Возврат НовыйКонтрагент.Ссылка;
		
	Иначе
		
		//Вернем ссылку на существующего контрагента:
		Возврат Ссылка;
		
	КонецЕсли;
//Конец Володин А.А. 29 октября 2024 г.
КонецФункции

&НаСервере
Процедура ПередатьФотоДоставокНаСервере();
//Начало Володин А.А. 28 октября 2024 г.
//Итоговая работа по модулю Мобильная разработка в 1С
	ЗапросОтправки = ФормированиеЗапроса(АдресЦентральнойБазы, "/hs/mobile/exchangedelivery");
	
	//Получим зарегистрированные данные:
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДоставкаИзменения.Ссылка.Комментарий КАК Комментарий,
			|	ФотоДоставок.Фотография КАК Фотография,
			|	ДоставкаИзменения.Ссылка КАК Ссылка,
			|	ДоставкаИзменения.Ссылка.Статус КАК Статус
			|ИЗ
			|	Документ.Доставка.Изменения КАК ДоставкаИзменения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФотоДоставок КАК ФотоДоставок
			|		ПО ДоставкаИзменения.Ссылка = ФотоДоставок.Доставка
			|ГДЕ
			|	ДоставкаИзменения.Узел = &Узел";
	
	Запрос.УстановитьПараметр("Узел", ЗапросОтправки.Узел);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	МассивДляОтправки = Новый Массив;
	МассивДляОтменыРегистрации = Новый Массив;
	
	КоличествоОтправленныхФото = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ДокументДоставки = Новый Структура;
		ДокументДоставки.Вставить("Ссылка", Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ДокументДоставки.Вставить("ФотоДоставки", Строка(Выборка.Фотография.Получить()));
		ДокументДоставки.Вставить("Комментарий", Выборка.Комментарий);
		ДокументДоставки.Вставить("Статус", Строка(Выборка.Статус));
		
		//Структуру документа в свою очередь добавим в массив для отправки:
		МассивДляОтправки.Добавить(ДокументДоставки);
		//Добавим в МассивДляОтменыРегистрации ссылку на подготовленный к отправке документ:
		МассивДляОтменыРегистрации.Добавить(Выборка.Ссылка);
		
		КоличествоОтправленныхФото = КоличествоОтправленныхФото + 1;
		
	КонецЦикла;
	
	//Сериализуем зарегистрированные данные:
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, МассивДляОтправки);
	ТелоЗапроса = ЗаписьJSON.Закрыть();
	
	ЗапросОтправки.ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветСервера = Соединение.ОтправитьДляОбработки(ЗапросОтправки.ЗапросHTTP);
	
	ОтветСервераСтрока = ОтветСервера.ПолучитьТелоКакСтроку();

	Если ОтветСервера.КодСостояния <> 200 Тогда
		
		ВызватьИсключение СтрШаблон("Ошибка: %1", ОтветСервераСтрока);
		
		Возврат;
		
	Иначе
		
		Если КоличествоОтправленныхФото = Число(ОтветСервераСтрока) Тогда
			
			Сообщить(СтрШаблон("Фото доставок отправлены в центральную базу. Отправлено: %1. Получено: %2", КоличествоОтправленныхФото, ОтветСервераСтрока));
			
			//Отменим регистрацию изменений если в массиве что-то есть:
			Если МассивДляОтменыРегистрации.Количество() <> 0 Тогда
				
				ПланыОбмена.УдалитьРегистрациюИзменений(ЗапросОтправки.Узел, МассивДляОтменыРегистрации); //Метод не проверяет статус ответа. Удаляет данные для регистрации не зависимо от успешности обмена.
				
			КонецЕсли;
				
		Иначе
			
			Сообщить(СтрШаблон("Повторите отправку в центральную базу. Отправлено: %1. Получено: %2", КоличествоОтправленныхФото, ОтветСервераСтрока));
			
		КонецЕсли;
	
	КонецЕсли; 
//Конец Володин А.А. 28 октября 2024 г.
КонецПроцедуры

#КонецОбласти
