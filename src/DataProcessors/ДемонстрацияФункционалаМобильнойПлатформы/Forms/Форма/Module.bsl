
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФотографияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Не СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
			
			Сообщить("Камера не доступна");
			Возврат;
			
		КонецЕсли;
		
		ДанныеФото = СредстваМультимедиа.СделатьФотоснимок(ТипКамерыУстройства.Задняя, , 50, Истина, ТипПодсветкиКамеры.Выключена);
		
		Если ДанныеФото = Неопределено Тогда
			
			Возврат;
			
		КонецЕсли;
		
		Фотография = ПоместитьВоВременноеХранилище(ДанныеФото.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УказатьКоординаты(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		ТекущийПровайдер = "gps";
		//ТекущийПровайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера(); //Можно выбрать энергоэкономичного провайдера
		
		Попытка
			
		Координаты = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ТекущийПровайдер);
		
		Исключение
		
		Координаты = Неопределено;
		
		КонецПопытки;
	
		Если Координаты = Неопределено Тогда
			
			ТекущийПровайдер = "network";
			
			Попытка
			
			Координаты = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ТекущийПровайдер);
			
			Исключение
			
			Координаты = Неопределено;
			
		КонецПопытки;
		
		КонецЕсли;
	
		Если Координаты = Неопределено Тогда
			
			Сообщить("Не удалось определить координаты.");
			Возврат;
			
		КонецЕсли;
		
		Если ТекущаяДата() - МестноеВремя(Координаты.Дата) > 300 Тогда
		
			СредстваГеопозиционирования.ОбновитьМестоположение(ТекущийПровайдер, 5);
			Координаты = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ТекущийПровайдер);
			
		КонецЕсли;
		
		Адрес = ПолучитьАдресПоМестоположению(Координаты.Координаты);
		
		Если Адрес <> Неопределено Тогда
			
			Местоположение = СтрШаблон("Координаты: Широта %1 Долгота %2
										|Страна: %3
										|Город: %4
										|Улица: %5
										|Дом: %6",
										Координаты.Координаты.Широта,
										Координаты.Координаты.Долгота,
										Адрес.Страна,
										Адрес.Город,
										Адрес.Улица,
										Адрес.Дом);

		Иначе
			
			Сообщить("Не удалось установить адрес по местоположению!");
			Местоположение = СтрШаблон("Координаты: Широта %1 Долгота %2", Координаты.Координаты.Широта, Координаты.Координаты.Долгота);
			
		КонецЕсли;
		
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура Позвонить(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Не СредстваТелефонии.ПоддерживаетсяНаборНомера() Тогда
			
			Сообщить("Набор номера не доступен");
			Возврат;
			
		КонецЕсли;
		
		СредстваТелефонии.НабратьНомер(Телефон, Истина); 
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьSMS(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Не СредстваТелефонии.ПоддерживаетсяОтправкаSMS(Истина) Тогда
			
			Сообщить("Отправка SMS не доступна");
			Возврат;
			
		КонецЕсли;
		
		SMSСообщение = Новый SMSСообщение();
		SMSСообщение.Получатели.Добавить(Телефон);
		SMSСообщение.Текст = СтрШаблон("Ваш заказ доставлен %1, по адресу: %2", Формат(ТекущаяДата(), "ДЛФ=DDT"), Местоположение);
		
		СредстваТелефонии.ПослатьSMS(SMSСообщение, Истина);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьEMail(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Не СредстваПочты.ПоддерживаетсяОтправка() Тогда
			
			Сообщить("Отправка почтового сообщения не доступна");
			Возврат;
			
		КонецЕсли;
		
		Письмо = Новый ИнтернетПочтовоеСообщение;
		Письмо.Получатели.Добавить(Email);
		Письмо.Тема =  СтрШаблон("Заказ доставлен");
		Письмо.Тексты.Добавить(СтрШаблон("Ваш заказ доставлен %1, по адресу: %2", Формат(ТекущаяДата(), "ДЛФ=DDT"), Местоположение));
		
		ИмяФайла = ПолучитьИмяВременногоФайла("jpg");
		ДвоичныеДанныеКартинки = ПолучитьИзВременногоХранилища(Фотография);
		Картинка = Новый Картинка(ДвоичныеДанныеКартинки);
		Картинка.Записать(ИмяФайла);
		
		Письмо.Вложения.Добавить(ИмяФайла, "Доставка.jpg");
		
		СредстваПочты.Послать(Письмо, ОбработкаТекстаИнтернетПочтовогоСообщения.Обрабатывать);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти


